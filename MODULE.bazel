module(name = "dv")

# =============================================================================
# Import Bazel Dependencies
# =============================================================================

bazel_dep(name = "platforms", version="1.0.0")
bazel_dep(name = "rules_rust", version = "0.67.0")
bazel_dep(name = "rules_rust_pyo3", version = "0.67.0")
bazel_dep(name = "rules_cc", version = "0.2.8")
bazel_dep(name = "rules_python", version = "1.6.3")
bazel_dep(name = "rules_uv", version = "0.88.0")
bazel_dep(name = "aspect_rules_js", version = "2.6.0")

# =============================================================================
# Rust Setup
# =============================================================================

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(edition = "2024")
use_repo(rust, "rust_toolchains")

# ==============================================================================
# PyO3 Setup
# ==============================================================================
register_toolchains(
    "@rules_rust_pyo3//toolchains:toolchain",
    "@rules_rust_pyo3//toolchains:rust_toolchain",
)

# ==============================================================================
# C/C++ Setup
# ==============================================================================

# Hermetic C/C++ toolchains (LLVM/Clang)
bazel_dep(name = "toolchains_llvm", version = "1.5.0")
llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm")
llvm.toolchain(
    name = "llvm_toolchain",
    llvm_version = "19.1.6",
)
use_repo(llvm, "llvm_toolchain")

# Register the LLVM cc toolchains so Bazel uses hermetic clang by default
register_toolchains(
    "@llvm_toolchain//:all",
)

# ==============================================================================
# Python Setup
# ==============================================================================

# Register Python 3.12 toolchain
python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(
    python_version = "3.12",
)
use_repo(python, "python_3_12")

# Set up pip dependencies
pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    hub_name = "pypi",
    python_version = "3.12",
    requirements_lock = "//:tools/requirements.txt",
)
use_repo(pip, "pypi")

# ==============================================================================
# JavaScript / Node.js Setup
# ==============================================================================

# Node.js version
# By default you get the node version from DEFAULT_NODE_VERSION in @rules_nodejs//nodejs:repositories.bzl
# Optionally you can pin a different node version:
bazel_dep(name = "rules_nodejs", version = "6.3.0")
node = use_extension("@rules_nodejs//nodejs:extensions.bzl", "node", dev_dependency = True)
node.toolchain(node_version = "20.13.1")

# Set up npm and pnpm package managers
npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm")
npm.npm_translate_lock(
    name = "npm",
    pnpm_lock = "//docs:pnpm-lock.yaml",
)
use_repo(npm, "npm")

# Allows developers to use the matching pnpm version, for example:
# bazel run -- @pnpm --dir $PWD install
pnpm = use_extension("@aspect_rules_js//npm:extensions.bzl", "pnpm")
use_repo(pnpm, "pnpm")
